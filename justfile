# talos dev task runner. Run `just` (or `just --list`) to see tasks.
#
# Enter the pinned toolchain first with `nix develop` (or `direnv allow`); these
# tasks assume the dev tools (cargo-nextest, cargo-deny, rumdl, shellcheck, selene,
# stylua, …)
# are on PATH. See docs/DEVELOPMENT.md.

# Default: show the task list.
default:
    @just --list

# Type-check everything.
check:
    cargo check --all

# Build the dev binaries (TUI + CLI).
build:
    cargo build --bin talos --bin talos-cli

# Run the full test suite (nextest).
test:
    cargo nextest run --all

# Run a single test by name: `just test-one perf_`.
test-one NAME:
    cargo nextest run -E 'test({{NAME}})'

# Run the bats suites: the install script, the pull-request-title checker, the
# winget packaging scripts and the Windows harness's psmux gate probes. Not
# part of `just test` (which is cargo's), and needs bats on PATH (the title
# checker's suite skips without `cog`).
test-scripts:
    bats scripts/install.bats
    bats scripts/ci/check-pr-title.bats
    bats packaging/winget/winget.bats
    bats scripts/dev/e2e/windows-vm.bats

# Format Rust + website code.
fmt:
    cargo fmt --all
    npm run fmt:website

# Lint everything CI lints (Rust + deny + markdown + shell + Lua).
lint:
    cargo fmt --all -- --check
    cargo clippy --all-targets --all-features -- -D warnings
    cargo deny check advisories
    cargo deny check bans licenses sources
    rumdl check .
    git ls-files -z '*.sh' | xargs -0 shellcheck
    selene ui examples
    stylua --check ui examples
    # Absolute path required: a relative --configpath resolves against the
    # server's install dir, and a missed config reports every injected global as
    # undefined instead of erroring.
    lua-language-server --check ui --configpath "{{ justfile_directory() }}/.luarc.json" --checklevel=Warning
    # The other direction: three panes that each misspell something, which must
    # each still come back as a finding.
    scripts/ci/check-lua-types.sh
    # And selene's own: one pane reading the injected tables, which must lint
    # clean, and one misspelt pane per table, which must not — so a table
    # declared without its fields fails instead of passing unread.
    scripts/ci/check-lua-std.sh

# Format the Lua interface in place (the counterpart to `cargo fmt`).
fmt-lua:
    stylua ui examples

# Architecture-rule + doc checks.
arch:
    cargo test --test architecture_rules
    RUSTDOCFLAGS="-D warnings" cargo doc --no-deps --all-features

# Install the non-Nix dev tools (fallback when not using the flake).
dev-tools:
    scripts/install-dev-tools.sh

# Install the prek git hooks.
hooks-install:
    prek install

# `cargo run` alone reads `~/.config/talos-dev/ui`, like every other config a dev
# build reads. Editing the interface in the repository is a different request, so it
# is stated here rather than inferred from the working directory.
#
# Run the dev TUI against THIS checkout's ui/ instead of your own copy.
tui-ui *ARGS:
    TALOS_UI_DIR="{{justfile_directory()}}/ui" cargo run --bin talos -- {{ARGS}}

# Run the dev TUI in the persistent default sandbox.
sandbox *ARGS:
    scripts/dev/sandbox.sh {{ARGS}}

# The interface lives inside the sandbox root, so a throwaway sandbox is already a
# clean interface directory. There is no separate recipe for that; there was one
# (`sandbox-fresh-ui`) which ran exactly this and claimed to do something else.

# Run the dev TUI in a throwaway sandbox (wiped on exit).
sandbox-fresh:
    scripts/dev/sandbox.sh --fresh

# A bare `sandbox` deliberately starts empty — the state most bugs are reported
# against. These opt in: one repository with a file of each git status, a session
# whose branch has changes, and one whose branch deliberately has none. Idempotent,
# so running either again costs a few lookups and changes nothing.

# Seed a demo repository + sessions into the persistent sandbox, then launch.
sandbox-demo:
    scripts/dev/sandbox.sh --demo

# As sandbox-demo, plus a 400-file repository whose diff is past the 4 MiB cap.
sandbox-demo-big:
    scripts/dev/sandbox.sh --demo-big

# Drop into a shell with the sandbox env (run `talos-cli …` by hand).
sandbox-shell:
    scripts/dev/sandbox.sh --shell

# Wipe a persistent sandbox profile (default: "default").
sandbox-clean PROFILE="default":
    scripts/dev/sandbox.sh --clean {{PROFILE}}

# Black-box TUI test: the real binary on a real pty (tests/tui_e2e.rs).
smoke:
    cargo nextest run --test tui_e2e

# Reap orphaned *test* tmux servers (Linux). A harness's own guard covers every
# in-process exit; this is for the ones a signal killed, which run on with no
# socket file and nothing able to connect to them. Never touches `talos` or
# `talos-dev`. `just reap-tmux --dry-run` lists without killing.
reap-tmux *ARGS:
    scripts/dev/reap-tmux-servers.sh {{ARGS}}

# Sweep with TALOS_BENCH_SESSIONS / _WIDTH / _HEIGHT.
# What a frame costs, piece by piece, against the real interface.
bench:
    cargo bench --bench frame_cost

# raw tmux vs Herdr vs talos as hosts for agent sessions (docs/BENCHMARK-MULTIPLEXERS.md).
# `just bench-multiplexers --quick --reps 1` to try it; the full run takes a while.
bench-multiplexers *ARGS:
    scripts/bench/run.sh {{ARGS}}

# Pass -s and -n explicitly: a reading only compares with one at the same size.
# What the whole binary costs under load: `just perf --idle`, `-n 19 -p 3 -s 255x62`, `-u 0`.
perf *ARGS:
    scripts/dev/perf-run.sh {{ARGS}}

# Drive tests against a real SSH host: `just lab <host> <verb>`.
lab HOST *ARGS:
    scripts/dev/e2e/real-host.sh {{HOST}} {{ARGS}}
